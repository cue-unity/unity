// Copyright 2021 The CUE Authors
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// package github declares the workflows for this project.
package github

import (
	"github.com/cue-unity/unity/internal/ci/base"
	"github.com/cue-unity/unity/internal/ci/gerrithub"

	"github.com/SchemaStore/schemastore/src/schemas/json"
)

// Note: the name of the workflows (and hence the corresponding .yml filenames)
// correspond to the environment variable names for gerritstatusupdater.
// Therefore, this filename must only be change in combination with also
// updating the environment in which gerritstatusupdater is running for this
// repository.
//
// This name is also used by the CI badge in the top-level README.
//
// This name is also used in the evict_caches lookups.
//
// i.e. don't change the names of workflows!
//
// In addition to separately declaring the workflows themselves, we define the
// shape of #workflows here as a cross-check that we don't accidentally change
// the name of workflows without reading this comment.
//
// We explicitly use close() here instead of a definition in order that we can
// cue export the github package as a test.
workflows: close({
	[string]: json.#Workflow

	trybot:             _
	daily_check:        _
	trybot_dispatch:    _
	unity:              _
	unity_dispatch:     _
	unity_cli_dispatch: _
})

_#testStrategy: {
	"fail-fast": false
	matrix: {
		"go-version": [_repo.latestStableGo]
		os: [_repo.linuxMachine]
	}
}

// _gerrithub is an instance of ./gerrithub, parameterised by the properties of
// this project
_gerrithub: gerrithub & {
	#repositoryURL:                      _repo.repositoryURL
	#botGitHubUser:                      "porcuepine"
	#botGitHubUserTokenSecretsKey:       "PORCUEPINE_GITHUB_PAT"
	#botGitHubUserEmail:                 "porcuepine@gmail.com"
	#botGerritHubUser:                   #botGitHubUser
	#botGerritHubUserPasswordSecretsKey: "PORCUEPINE_GERRITHUB_PASSWORD"
	#botGerritHubUserEmail:              #botGitHubUserEmail
}

// _base is an instance of ./base, parameterised by the properties of this
// project
//
// TODO: revisit the naming strategy here. _base and base are very similar.
// Perhaps rename the import to something more obviously not intended to be
// used, and then rename the field base?
_base: base & {
	#repositoryURL:                "https://github.com/cue-lang/cue"
	#defaultBranch:                _repo.defaultBranch
	#botGitHubUser:                "porcuepine"
	#botGitHubUserTokenSecretsKey: "PORCUEPINE_GITHUB_PAT"
}
